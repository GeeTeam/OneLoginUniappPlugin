plugins {
    id 'com.android.library'
}

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion "${BUILD_TOOLS_VERSION}"

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode VERSION_CODE as int
        versionName "${VERSION_NAME}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'onelogin-proguard-rules.pro'
    }

    buildTypes {
        release {
            //混淆
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            //混淆
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions{
        doNotStrip "*/armeabi/*.so"
        doNotStrip "*/armeabi-v7a/*.so"
        doNotStrip "*/x86/*.so"
        doNotStrip "*/arm64-v8a/*.so"
        doNotStrip "*/x86_64/*.so"
    }
    android.libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFileName
            if (outputFile != null && outputFile.endsWith('.aar')) {
                // 显示app名称+当前版本号

                def fileName = "geetest_oneloginuni_android_v${defaultConfig.versionName}"
                // 加入打包时间
                fileName = fileName + "_${releaseTime()}"
                // 如果是debug版本，加入beta版本号
                if ('debug'.equals(variant.buildType.name)) {
                    fileName = fileName + "_debug.aar"
                } else {
                    // 如果是release版本，直接显示
                    fileName = fileName + ".aar"
                }
                output.outputFileName = fileName
            }
        }
    }
    repositories {
        flatDir {
            dirs 'libs'
        }
    }
}

// 显示的时间格式，形如20171110
def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getDefault())
}

dependencies {

    compileOnly fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly fileTree(dir: '../app/libs', include: ['uniapp-v8-release.aar'])

    implementation 'com.alibaba:fastjson:1.2.75'

    implementation 'com.geetest.android:onelogin:2.7.4'
}